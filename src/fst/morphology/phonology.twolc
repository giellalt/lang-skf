!! =================================== !
!! The Sakirabiá morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 ã ẽ ĩ õ ũ
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ý ç ɨ
 kʷ

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
 Ã Ẽ Ĩ Õ Ũ
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ý Ç Ɨ
 Kʷ

 ː ! IPA vowel length (U+02D0) Modifier letter triangular colon
 ' ! the apostrophe is a glotal stop marker
 ʼ ! the Modifier letter apostrophe (U+02BC) is a glotal stop marker
 ˈ ! Modifier letter vertical line (U+02C8) is IPA primary stress marker
 ˌ ! Modifier letter low vertical line (U+02CC) is IPA secondary stress marker

!! Archiconsonants
%{pbØ%}:p !
!%{pbØ%}:b
%{tr%}:t
!%{tr%}:r
%{kg%}:k
!%{kg%}:g
%{bmØ%}:b ! after oral vow
!%{bmØ%}:m ! after nasal vow
!%{bmØ%}:0 ! after cns
%{õo%}:õ
!%{õo%}:o

%{eV%}:e
%{eØ%}:e
!%{eØ%}:0

! triggers
%^WG:0

! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <


 %>
 %<
 #
 «
 »

  ;

Sets

 Vow     = a e i o u ã ẽ ĩ õ ũ 
           %{õo%} %{eV%} %{eØ%} ;
 OralVow = a e i o u ;
 NasVow  = ã ẽ ĩ õ ũ ;
 Cns     = b c d f g h j k l m n p q r s t v w x z '
           %{pbØ%} %{tr%} %{kg%} %{bmØ%} ;
 VowInit = %{eV%} %{eØ%} ;

Rules

!"Vowel lengthening at morpheme boundary"
!Vx:Vy <=> Vy %^WG: %> _ ;
!     where Vx in (%{eV%} %{eV%} %{eV%} %{eV%} %{eV%} %{eV%} %{eV%} %{eV%} %{eV%} %{eV%} )
!           Vy in (   a      e      i      o      u      ã      ẽ      ĩ      õ      ũ   ) matched ;
 
" %{eV%}:o"
 %{eV%}:o <=> o %^WG: %> _ ;
!!

"Weaken pb when followed by morpheme on-set vowel"
Cx:Cy <=> [Vow - o] _ %^WG:0 [ %> | %< ] [Vow | VowInit: | %{bmØ%}:];
             o _ %^WG:0 [ %> | %< ] %{bmØ%}: ;
     where Cx in (%{pbØ%})
           Cy in (   b   ) matched ;

"pb to 0"
%{pbØ%}:0 <=> o _ %^WG:0 [ %> | %< ] [Vow | VowInit: ] ;


"Weaken stem-final stop when followed by morpheme on-set vowel"
Cx:Cy <=> _ %^WG:0 [ %> | %< ] [Vow | VowInit: | %{bmØ%}:];
     where Cx in (%{tr%} %{kg%})
           Cy in (   r      g ) matched ;

!! tek+N+Sg+Ill+PxSg1
!!€ o%<te%{kg%}%^WG%>%{bmØ%}%{õo%}
!!€ o%<teg0%>0õ

"Strengthen when followed by voiceless "
Cx:Cy <=> _ %^WG:0 [ %> | %< ] [s| Cns] ;!CHECKME 20230929
          _ # ;
     where Cx in (%{pbØ%} %{tr%} %{kg%})
           Cy in (   p      t      k ) matched ;

"%{eØ%}:0 loss following stem-final vowel"
 %{eØ%}:0 <=> Vow %^WG:0 %> _ ;
!! 


"%{bmØ%} is m after nasal"
%{bmØ%}:m <=> NasVow (%^WG:) %> _ ;

"%{bmØ%} is 0 after consonant"
%{bmØ%}:0 <=> Cns: (%^WG:) %> _ ;

!!TESTS
!echo 'o < o p o ^WG:0 > %{eV%}:o r i' | hfst-pair-test src/phonology/skf-phon.compose.hfst
!echo 'opo+N+Sg+Abl+PxSg1' | hfst-lookup src/morphology/lexicon.hfst

